@echo off
rem Copyright 2007 TeX Users Group.
rem You may freely use, modify and/or distribute this file.

rem deal with use of 'amstex.ini' instead of 'amstex'
if "%1" == "amstex.ini" %0 amstex


echo %1 | find "." > NUL
if errorlevel 1 goto noextens
echo Please do not use an extension (%1) when specifying file name
goto endpause

:noextens
rem switch to temporary directory
C:\DVIWINDO\cdd.com e:\
rem deal with use of 'amstex' instead of 'amstex.ini'
if not "%1" == "amstex" goto notamstex
call tex -i amstex.ini
goto inidone

:notamstex
rem deal with the fact that LateX2e requires TWO iniTeX runs
if not "%1" == "latex" goto notlatex
call tex -i unpack.ins
if errorlevel = 1 goto inidone
call tex -i %1.ltx
goto inidone

:notlatex
rem generic case
call tex -i %1

:inidone
rem DON'T mess with files if TeX had a cow ...
if not errorlevel = 1 goto seemsok
echo Sorry, TeX appears to be have become unhappy --- formats will not be saved
goto endpause

:seemsok
echo copying new format file to c:\yandytex\fmt
copy %1.fmt c:\yandytex\fmt
copy %1.log c:\yandytex\fmt

echo deleting new format file from working directory
del %1.log
del %1.fmt

:skipdel

rem deal with 'amstex.ini' special case (want +amstex, not +amstex.ini)
if not "%1" == "amstex.ini" goto notams
echo %2=tex.bat +amstex @.tex>> C:\WINDOWS.000\dviwindo.ini
goto end

:notams
rem deal with 'plain' special case (omit format, since plain is default)
if not "%1" == "plain" goto notplain
echo %2=tex.bat @.tex>> C:\WINDOWS.000\dviwindo.ini
goto end

:notplain
echo %2=tex.bat +%1 @.tex>> C:\WINDOWS.000\dviwindo.ini
goto end

:endpause
if not errorlevel 1 goto end

rem pause, but only when run in DOS box in Windows
set | find "windir=" > NUL
if not errorlevel 1 pause

:end
